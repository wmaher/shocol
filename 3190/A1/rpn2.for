C TRANSLATING ALGRBRAIC EXPRESSIONS TO POLISH NOTATION
C
C THE VARIABLE NAMES AND THEIR MEANINGS ARE AS FOLLOWS:
C     SOURCE     THE INPUT STRING, IN NORMAL ALGEBRAIC FORM
C     SHIER      ARRAY CONTAINING THE HIERARCHY NUMBERS OF THE INPUT
C     OPSTCK     'OPERATOR STACK': THE OPERATORS FROM THE INPUT
C     OHIER      ARRAY CONTAINING THE HIERARCHY NUMBERS OF THE OPERATORS
C     POLISH     THE OUTPUT STRING, IN POLISH NOTATION
C
C     L          DO INDEX USED IN INITIALIZING
C     M          DO INDEX USED IN SETTING UP SHIER ARRAY
C     I          POINTER TO INDEX STRING (SOURCE AND SHIER)
C     J          POINTER TO OPERATOR STACK (OPSTCK AND OHIER)
C     K          POINTER TO OUTPUT STRING (POLISH)
C
C     THE OTHER VARIABLES ARE ACTUALLY CONSTANTS, AND ARE
C     DEFINED IN THE DATA STATEMENT.
C
C
      INTEGER*1 SOURCE(40), SHIER(40), OPSTCK(40), OHIER(40), POLISH(40)
      INTEGER*1 BLANK, LPAREN, RPAREN, PLUS, MINUS, ASTRSK, SLASH
      DATA BLANK/1H /, LPAREN/1H(/, RPAREN/1H)/, PLUS/1H+/, 
     1     MINUS/1H-/, ASTRSK/1H*/, SLASH/1H//
C
C INITIALIZE ARRAYS TO ZERO OR BLANK, AS APPROPRIATE
  10  DO 20 L = 1, 40
      SHIER(L) = 0
      OHIER(L) = 0
      OPSTCK(L) = BLANK
      POLISH(L) = BLANK
  20  CONTINUE
C
C READ A 'DATA' CARD
      READ (*, 30) SOURCE
  30  FORMAT (40A)
C
C IN THE FOLLOWING DO-LOOP, M POINTS TO INPUT COLUMNS, FROM LEFT TO RIGHT
C FIRST BLANK SIGNALS END OF STRING (EMBEDDED BLANKS ARE NOT ALLOWED)
C IT IS ASSUMED THAT IF A CHARACTER IS NOT AN OPERATOR OR A
C PARENTHESIS, IT IS A VARIABLE.
      DO 40 M = 1, 40
      IF (SOURCE(M) .EQ. BLANK) GO TO 60
C
C SET SHIER(M) TO ZERO, THEN CHANGE IT IF THE CHARACTER IS AN OPERATOR
C     SHIER(M) = 0
      IF (SOURCE(M) .EQ. LPAREN) SHIER(M) = 1
      IF (SOURCE(M) .EQ. RPAREN) SHIER(M) = 2
      IF (SOURCE(M) .EQ. PLUS
     1    .OR. SOURCE(M) .EQ. MINUS) SHIER(M) = 3
      IF (SOURCE(M) .EQ. ASTRSK 
     1    .OR. SOURCE(M) .EQ. SLASH) SHIER(M) = 4
  40  CONTINUE
C
C IF NORMAL EXIT IS TAKEN, THE CARD DID NOT CONTAIN A BLANK
      WRITE (*,50)
  50  FORMAT (1X, 'DATA INPUT IN ERROR - NO BLANKS')
      GO TO 10
C
C IF SOURCE-STRING POINTER = 1 ON EXIT FROM DO, INPUT WAS BLANK
  60  IF (M .EQ. 1) STOP 
C
C OTHERWISE PROCEED TO TRANSLATION
C INITIALIZE HIERARCHY NUMBERS TO GET STARTED PROPERLY
      SHIER(M) = 0
      OHIER(1) = -1
C
C INITIALIZE POINTERS
      I = 1
      J = 2
      K = 1
C
C CHECK FOR OPERAND
  70  IF ( SHIER(I) .EQ. 0 ) GO TO 90
C
C CHECK FOR RIGHT PARENTHESIS
      IF ( SHIER(I) .EQ. 2 ) GO TO 80
C
C SOME OTHER OPERATOR IF HERE -- MOVE TO OPERATOR STACK
      OPSTCK(J) = SOURCE(I)
      OHIER(J) = SHIER(I)
C
C ADVANCE POINTERS
      I = I + 1
      J = J + 1
      GO TO 70
C
C DELETE CORRESPONDING LEFT PARENTHESIS
  80  I = I + 1
      J = J - 1
      GO TO 100
C
C MOVE OPERAND TO POLISH STRING
  90  POLISH(K) = SOURCE(I)
      I = I + 1
      K = K + 1
C
C CHECK HIERARCHY RANKINGS
 100  IF ( OHIER(J-1) .GE. SHIER(I) ) GO TO 110
C
C CHECK FOR END OF SOURCE STRING
      IF ( I .EQ. M ) GO TO 120
      GO TO 70
C
C MOVE OPERATOR TO POLISH STRING
 110  POLISH(K) = OPSTCK(J-1)
      K = K + 1
      J = J - 1
      GO TO 100
C
C WRITE SOURCE AND POLISH STRINGS
 120  WRITE (*, 130) "INPUT: ", SOURCE, "RPN:   ", POLISH
 130  FORMAT (1H ,A7, 40A1/1H , A7, 40A1)
      GO TO 10
      END 
